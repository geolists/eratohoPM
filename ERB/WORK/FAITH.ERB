;=========================================
;布教コマンド
;信仰心を溜めたり人形を作ったり……
;=========================================
@MOVE_FAITH
#DIM GET_FAITH
#DIM GET_VALUE
#DIM L_LOCAL, 1
;製作対象
;(上昇対象保存先としても使う)
#DIM TAG, 1
;消費信仰心
#DIM CON_FAITH, 3

$INPUT_LOOP_0
DRAWLINE
PRINTL ～～礼拝の間～～
PRINTFORM %CALLNAME:TARGET%の信仰力:
CALL SETCOLOR_FAITH
PRINTFORML {BASE:信仰力}
RESETCOLOR
PRINTFORML 何を行いますか？
DRAWLINE
PRINTL [0] - お守りを与える
PRINTL [1] - 身体を捧げる
PRINTL [2] - 人形を作る
PRINTL [3] - 神降ろしを行う
PRINTL [4] - 戻る

CALL SENTAKUSHI, 5

DRAWLINE
IF RESULT == 0
	CALL CALCULATION_FAITH, 1, 0, 0
	GET_FAITH = RESULT
	GET_VALUE = BASE:信仰 / 10
	
	PRINTFORML 信仰力入手の期待値は{GET_FAITH*92/100}
	PRINTFORML 評価入手の期待値は{GET_VALUE*92/100}です
	PRINTL 実行しますか？
	PRINTL [0] - はい
	PRINTL [1] - いいえ
	
	CALL SENTAKUSHI, 2
		
	IF RESULT == 1
		GOTO INPUT_LOOP_0
	ENDIF
	
	PRINTFORML %CALLNAME:TARGET%は困っている人々にお守りを与え歩いた……
	GET_FAITH = GET_FAITH * (85 + RAND:16) / 100
	GET_VALUE = GET_VALUE * (85 + RAND:16) / 100
	CALL SETCOLOR_VALUE
	PRINTFORML 評価が{GET_VALUE}上がった！
	CALL SETCOLOR_FAITH
	PRINTFORMW 信仰力が{GET_FAITH}上がった！
	RESETCOLOR
	
	CFLAG:評価 += GET_VALUE
	BASE:信仰力 = LIMIT(BASE:信仰力+GET_FAITH, 0, MAXBASE:信仰力)
	BEGIN TURNEND
ELSEIF RESULT == 1
	CALL CALCULATION_FAITH, 1, 1, 0
	GET_FAITH = RESULT
	
	PRINTFORML 信仰力入手の期待値は{GET_FAITH*92/100}です
	PRINTFORML 淫妖経験や淫妖も上昇します
	PRINTL 実行しますか？
	PRINTL [0] - はい
	PRINTL [1] - いいえ
	
	CALL SENTAKUSHI, 2
		
	IF RESULT == 1
		GOTO INPUT_LOOP_0
	ENDIF
	
	GET_FAITH = GET_FAITH * (85 + RAND:16) / 100
	PRINTFORML %CALLNAME:TARGET%は道行く人々に奉仕をして回った……
	PRINTFORML 淫妖経験 + 2
	PRINTFORML 精液経験 + 2
	PRINTFORML 精飲経験 + 2
	CALL SETCOLOR_FAITH
	PRINTFORMW 信仰力が{GET_FAITH}上がった！
	RESETCOLOR
	
	EXP:淫妖経験 += 2
	EXP:精液経験 += 2
	EXP:精飲経験 += 2
	CALL CAL_CHASTE, 1, "下降"
	BASE:信仰力 = LIMIT(BASE:信仰力+GET_FAITH, 0, MAXBASE:信仰力)
	
	CALL CALCULATION_INYOU, 9, 90
	
	PRINTFORM 淫妖:{BASE:淫妖}　→　
	BASE:淫妖 += RESULT
	CALL SETCOLOR_PALAMUP
	PRINTFORMW {BASE:淫妖}
	RESETCOLOR
	BEGIN TURNEND
ELSEIF RESULT == 2
	$INPUT_LOOP_1
	
	LOCAL = 10000
	LOCAL:1 = 19000
	LOCAL:2 = 37000
	
	;コスト管理を注力している場合、半額になる
	IF CHECKSKILL(311)
		LOCAL /= 2
		LOCAL:1 /= 2
		LOCAL:2 /= 2
	ENDIF
	
	CON_FAITH = 50
	CON_FAITH:1 = 110
	CON_FAITH:2 = 200
	
	DRAWLINE
	FOR L_LOCAL, 0, 3
		PRINTFORM [{L_LOCAL, 1, RIGHT}] - %ITEMNAME:(530+L_LOCAL), 10, LEFT% 
		CALL SETCOLOR_MONEY
		PRINTFORM ${LOCAL:L_LOCAL}
		RESETCOLOR
		PRINTFORML 　消費信仰力:{CON_FAITH:L_LOCAL, 3, RIGHT}　使うと評価が{100*(L_LOCAL+1)}上昇する
	NEXT
	PRINTL [3] - 戻る
	
	CALL SENTAKUSHI, 4
	
	IF RESULT == 0
		TAG = 0
	ELSEIF RESULT == 1
		TAG = 1
	ELSEIF RESULT == 2
		TAG = 2
	ELSEIF RESULT == 3
		GOTO INPUT_LOOP_0
	ENDIF
	
	
	IF MONEY < LOCAL:TAG
		PRINTL お金が足りません
		GOTO INPUT_LOOP_1
	ELSEIF BASe:信仰力 < CON_FAITH:TAG
		PRINTL 信仰力が足りません
		GOTO INPUT_LOOP_1
	ENDIF
	PRINTFORM %ITEMNAME:(530+TAG)%の作成には
	CALL SETCOLOR_MONEY
	PRINTFORM ${LOCAL:TAG}
	RESETCOLOR
	PRINTFORML と{CON_FAITH:TAG}の信仰力が必要です
	PRINTFORML 作成しますか？
	PRINTL [0] - はい
	PRINTL [1] - いいえ
	
	CALL SENTAKUSHI, 2
	
	IF RESULT == 0
		PRINTFORML %ITEMNAME:(530+TAG)%を作成した！
		ITEM:(530+TAG) += 1
		MONEY -= LOCAL:TAG
		BASE:信仰力 -= CON_FAITH:TAG
		BEGIN TURNEND
	ELSEIF RESULT == 1
		GOTO INPUT_LOOP_1
	ENDIF
ELSEIF RESULT == 3
	$INPUT_LOOP_2
	
	CON_FAITH = 2000
	CON_FAITH:1 = 1000
	CON_FAITH:2 = 3000
	
	DRAWLINE
	PRINTL どの神様を降ろしますか？
	PRINTFORM [0] - 気質の神　消費信仰:
	CALL SETCOLOR_FAITH
	PRINTFORM {CON_FAITH,5,RIGHT}　
	RESETCOLOR
	PRINTFORML 感度・依存以外の気質を80上昇させます
	
	PRINTFORM [1] - 回復の神　消費信仰:
	CALL SETCOLOR_FAITH
	PRINTFORM {CON_FAITH:1,5,RIGHT}　
	RESETCOLOR
	PRINTFORML 疲労度を0にします
	
	PRINTFORM [2] - 天啓の神　消費信仰:
	CALL SETCOLOR_FAITH
	PRINTFORM {CON_FAITH:2,5,RIGHT}　
	RESETCOLOR
	PRINTFORML いずれかの能力を上昇させます(淫妖・堕落以外)
	PRINTL [3] - 戻る
	
	CALL SENTAKUSHI, 4
	
	IF RESULT < 3 && BASE:信仰力 < CON_FAITH:RESULT
		PRINTL 信仰力が足りません
		GOTO INPUT_LOOP_2
	ELSEIF RESULT == 0
		$BACK_LOOP_KISITU
		DRAWLINE
		PRINTL どの気質を上昇させますか？
		FOR L_LOCAL, 0, 8
			;感度と依存は除外
			IF L_LOCAL == 2 || L_LOCAL == 8
				L_LOCAL += 1
			ENDIF
			PRINTFORML [{L_LOCAL}] - %GETNAME_KISITU(L_LOCAL), 8, LEFT%:{BASE:GETNAME_KISITU(L_LOCAL)}
		NEXT
		PRINTFORML [{L_LOCAL}] - 戻る
		
		$INPUT_LOOP_KISITU
		INPUT
		
		IF RESULT == L_LOCAL
			GOTO INPUT_LOOP_2
		ELSEIF RESULT < L_LOCAL && RESULT >= 0 && !(RESULT == 2 || RESULT == 8)
			DRAWLINE
			TAG = RESULT
			PRINTFORM %GETNAME_KISITU(RESULT)%:{BASE:GETNAME_KISITU(RESULT)}　→　
			LOCAL = LIMIT(BASE:GETNAME_KISITU(RESULT)+80, 0, 600)
			CALL SETCOLOR_PALAMUP
			PRINTFORML {LOCAL}
			RESETCOLOR
			PRINTL 
			PRINTL 以上のような変化をします
			PRINTL よろしいですか？
			PRINTL [0] - はい
			PRINTL [1] - いいえ
			
			CALL SENTAKUSHI, 2
			
			IF RESULT == 0
				PRINTFORML %GETNAME_KISITU(TAG)%を上昇させた！
				PRINTFORM %GETNAME_KISITU(TAG)%:{BASE:GETNAME_KISITU(TAG)}　→　
				CALL SETCOLOR_PALAMUP
				PRINTFORML {LOCAL}
				BASE:GETNAME_KISITU(TAG) = LOCAL
				BASE:信仰力 -= CON_FAITH
				RESETCOLOR
				BEGIN TURNEND
			ELSEIF RESULT == 1
				GOTO BACK_LOOP_KISITU
			ENDIF
		ELSE
			PRINTL 入力が不正です
			GOTO INPUT_LOOP_KISITU
		ENDIF
	ELSEIF RESULT == 1
		LOCAL = RESULT
		DRAWLINE
		PRINTFORM 疲労度:{BASE:ストレス}　→　
		CALL SETCOLOR_PALAMUP
		PRINTL 0
		RESETCOLOR
		PRINTL 
		PRINTL 以上のような変化をします
		PRINTL よろしいですか？
		PRINTL [0] - はい
		PRINTL [1] - いいえ
		
		CALL SENTAKUSHI, 2
		
		IF RESULT == 0
			PRINTFORML 疲労度が0になった！
			BASE:ストレス = 0
			BASE:信仰力 -= CON_FAITH:LOCAL
			BEGIN TURNEND
		ELSEIF RESULT == 1
			GOTO INPUT_LOOP_2
		ENDIF
	ELSEIF RESULT == 2
		$BACK_LOOP_PALAM
		DRAWLINE
		PRINTL どの能力を上昇させますか？
		FOR L_LOCAL, 0, 10
			;没能力or関係無いステータスは飛ばす
			;淫妖＆堕落も飛ばす
			IF L_LOCAL >= 6
				CONTINUE
			ENDIF
			CALL PALAM_CONVERT_NAME, L_LOCAL
			PRINTFORML [{L_LOCAL}]%STR%:{BASE:GETNAME_PALAM(L_LOCAL), 4, RIGHT}
		NEXT
		PRINTFORML [{L_LOCAL}] - 戻る
		
		$INPUT_LOOP_PALAM
		INPUT
		
		IF RESULT == L_LOCAL
			GOTO INPUT_LOOP_2
		ELSEIF RESULT < L_LOCAL && RESULT >= 0 && !(RESULT >= 6 && RESULT <= 9)
			DRAWLINE
			TAG = RESULT
			PRINTFORML %GETNAME_PALAM(TAG)%を変化させます
			PRINTL よろしいですか？
			PRINTL [0] - はい
			PRINTL [1] - いいえ
			
			CALL SENTAKUSHI, 2
			
			IF RESULT == 0
				PRINTFORML %GETNAME_PALAM(TAG)%を上昇させた！
				CALL CALCULATION_PALAM, 50, GETNAME_PALAM(TAG)
				BASE:信仰力 -= CON_FAITH:2
				BEGIN TURNEND
			ELSEIF RESULT == 1
				GOTO BACK_LOOP_KISITU
			ENDIF
		ELSE
			PRINTL 入力が不正です
			GOTO INPUT_LOOP_PALAM
		ENDIF
	ELSEIF RESULT == 3
		GOTO INPUT_LOOP_0
	ENDIF
ELSEIF RESULT == 4
	RETURN 0
ENDIF

