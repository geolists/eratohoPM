;[ライセンス]パブリックドメイン
;本ファイルに係る著作権を放棄する。
;本ファイルに係る著作人格権は行使しない。
;2009/6/13 MinorShift(Emuera作者)

;独自のタイトル画面の例（Emuera ver 1.700以降対象）
;LOADGAME_EXが定義されていることが前提なので、SAVELOAD.ERBと同時に使用してください。
;SAVELOAD.ERBを使用しない場合、CALL LOADGAME_EXをLOADGAMEに置き換えてください。
;なお、SYSTEM_TITLEが定義されているとTITLE_LOADGAMEは呼ばれなくなります。
;使用している命令、変数の詳細についてはEmueraの拡張文法.txtを参照してください。

@SYSTEM_TITLE
;@SYSTEM_TITLEが定義されていると、標準のタイトル画面の代わりにSYSTEM_TITLEが呼ばれる。

;このタイミングでグローバル変数を読んでおけば取りこぼしが無くなる。
;GLOBALはRESETDATAやLOADDATAによって初期化・上書きされない。
;必要に応じてコメントアウトを解除すること。
LOADGLOBAL
;起動回数+1
GLOBAL:500 += 1
;初回起動時、画像表示の初期設定を1(表示(大))にする
IF GLOBAL:500 == 1
	GLOBAL:4 = 1
ENDIF
SAVEGLOBAL

;1/255の確率でおまけモードをプレイできる
IF !RAND:255
	LOADGLOBAL
	;隠しフラグを立てる
	GLOBAL:999 = 1
	SAVEGLOBAL
ENDIF

RESETBGCOLOR

;バージョン表記をLOCALS:0に作成。
;1001なら1.001、1100なら1.10と表示されるように。
;ここで使用した変数はRESETDATAかLOADDATAによってクリアされるのでローカル変数を使わなくてもよい。
LOCAL:0 = GAMEBASE_VERSION / 1000
LOCAL:1 = GAMEBASE_VERSION % 1000
STRLENFORM {LOCAL:1}
LOCALS:1 = %"0" * (3 - RESULT:0)%{LOCAL:1}
LOCALS:0 = v{LOCAL:0}.%LOCALS:1%
SIF LOCAL:2 != 0
	LOCALS:0 = %LOCALS:0%.{LOCAL:2}

;タイトル表示。
DRAWLINE 

ALIGNMENT CENTER
TRYCALL CHECK_DEBUG_PM
IF RESULT == 100
	CALL SETCOLOR_ABL
	PRINTFORML (デバッグ用)
ENDIF
PRINTFORML %GAMEBASE_TITLE%
PRINTFORML %LOCALS:0%
PRINTFORML %GAMEBASE_AUTHOR%
PRINTFORML (%GAMEBASE_YEAR%)
;起動回数50回になると星が見えるようになり、クリックすると新しいダンジョンが出現する
IF GLOBAL:500 >= 50 && !GLOBAL:998
	SETCOLOR 255, 192, 64
	PRINTFORML [100]☆
ELSEIF GLOBAL:998
	SETCOLOR 255, 192, 64
	PRINTFORML ミミミ☆　ミミミ☆　ミミミ☆　ミミミ☆
ELSE
	PRINTL 
ENDIF
RESETCOLOR
PRINTFORML %GAMEBASE_INFO%
ALIGNMENT LEFT

DRAWLINE 

;選択肢表示。_Rename.csvで指定した値を読むことはできないのでここで指定する必要がある。
$TITLE_SELECT
PRINTL [0] 最初からはじめる
PRINTL [1] ロードしてはじめる
IF GLOBAL:999 || CHECKEXTRAGAME()
	IF GLOBAL:999
		CALL SETCOLOR_MONEY
	ENDIF
	PRINTL [2] ？？？
	RESETCOLOR
ENDIF

$TITLE_INPUT
INPUT
IF RESULT == 0
	RESETDATA
	ADDCHARA 0
	SIF GAMEBASE_DEFAULTCHARA >= 0
		ADDCHARA GAMEBASE_DEFAULTCHARA
	BEGIN FIRST
	RETURN
ELSEIF RESULT == 1
	CALL LOADGAME_EX
	RESTART
	;LOADGAME_EXでLOADを行わずに戻ってきた場合、もう一度選択しなおす。
ELSEIF RESULT == 100 && GLOBAL:500 >= 50 && !GLOBAL:998
	PRINTFORML 月の都に何かが起きたようだ……

	LOADGLOBAL
	GLOBAL:998 = 1
	SAVEGLOBAL
	RESTART
ELSEIF RESULT == 2 && (GLOBAL:999 || CHECKEXTRAGAME())
	RESETDATA
	ADDCHARA 0
	SIF GAMEBASE_DEFAULTCHARA >= 0
		ADDCHARA GAMEBASE_DEFAULTCHARA
	CALL EXTRA_MODE
	IF RESULT == 1
		RESTART
	ENDIF
	BEGIN FIRST
	RETURN
;9/8は輝夜の日とかどうとか
ELSEIF RESULT == 98
	CALL HIDDENTEXT,1
	RESTART
;8556。他にいい数字を思いつかなかった
ELSEIF RESULT == 8556
	CALL HIDDENTEXT, 3
	RESTART
ELSEIF RESULT == 815
	CALL HIDDENGAME
	RESTART
;ワールブルクがノーベル賞を受賞した年
ELSEIF RESULT == 1931
	CALL HIDDENTEXT, 2
	RESTART
;とある場所の郵便番号
ELSEIF RESULT == 6350823
	;適当に文章を垂れ流す
	CALL HIDDENTEXT, 0
	RESTART
;とあるグローバル変数が特定の条件を満たすと入れる
;現在入る方法なし。内容の実装もなし
ELSEIF GLOBAL:995 & 0xfbffffff && RESULT == 1000
	CALL LOCKBOX
ELSE
	REUSELASTLINE 無効な値です
	GOTO TITLE_INPUT
ENDIF

;SYSTEM_TITLE中でRETURNすると次に実行する行が無くなり、エラーになる。
;必ずBEGIN命令かLOADGAMEを行うこと。


