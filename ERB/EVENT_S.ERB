;EVENT_S.ERB、EVENT_SUB、追加処理及びサブイベント
;-------------------------------------------------
;日付が変わった時のイベント
;-------------------------------------------------
@EVENT_NEXTDAY
$INPUT_LOOP
;不思議の根持ちで精液経験が300以上で生える
IF TARGET >= 0 
	IF TALENT:TARGET:138 == 1 && EXP:TARGET:20 >= 300 && TALENT:139 == 0 && TALENT:140 == 0
		DRAWLINE
		PRINTFORML ……どうやら[不思議な根]の効果が現れそうだ
		PRINTFORMW %CALLNAME:TARGET%を[ふたなり]にしますか？
		PRINTL [0] - する
		PRINTL [1] - しない
		INPUT
		IF RESULT == 0
			PRINTFORML %NAME:TARGET%の[不思議な根]は[ふたなり]となった
			TALENT:139 = 1
		ELSEIF RESULT == 1
			PRINTFORML %NAME:TARGET%は[不思議な根]を失った
		ELSE
			GOTO INPUT_LOOP
		ENDIF
		DRAWLINE
		WAIT
		TALENT:TARGET:138 = 0
	ENDIF
ENDIF

$INPUT_LOOP_2
;具現持ちでレズ経験2500以上で生える
IF TARGET >= 0 
	IF TALENT:TARGET:146 == 1 && EXP:TARGET:40 >= 2500 && TALENT:139 == 0 && TALENT:140 == 0
		DRAWLINE
		PRINTFORML ……どうやら[具現]の効果が現れそうだ
		PRINTFORML %CALLNAME:TARGET%を[ふたなり]にしますか？
		PRINTL [0] - する
		PRINTL [1] - しない
		INPUT
		IF RESULT == 0
			PRINTFORML %NAME:TARGET%は[具現]によって[ふたなり]となった
			TALENT:139 = 1
		ELSEIF RESULT == 1
			PRINTFORML %NAME:TARGET%は[具現]を失った
		ELSE
			GOTO INPUT_LOOP_2
		ENDIF
		DRAWLINE
		WAIT
		TALENT:TARGET:146 = 0
	ENDIF
ENDIF

;放尿経験３０以上でおもらし癖がつく
IF TARGET >= 0 
	IF TALENT:57 == 0
		IF (TALENT:TARGET:151 && EXP:TARGET:31 >= 10) || (TALENT:TARGET:151 == 0 && EXP:TARGET:31 >= 30)
			PRINTFORMW （む･･･なんだこれ？）
			PRINTFORM %CALLNAME:TARGET%は
			SIF TALENT:TARGET:151 && EXP:TARGET:31 >= 10
			PRINTFORM おねしょを
			SIF TALENT:TARGET:151 == 0 && EXP:TARGET:31 >= 30 
			PRINTFORM 粗相を
			PRINTFORMW してしまったようだ……
			PRINTFORMW %CALLNAME:TARGET%は[おもらし癖]がついた
			TALENT:TARGET:57 = 1
		ENDIF
	ENDIF
ENDIF

IF TARGET >= 0 
	;反発刻印３があり、かつ幼稚である時に
	IF TALENT:TARGET:151 && MARK:3 == 3
		;欲望が5かつ従順が5かつマゾっ気が5かつ異常経験が７以上になる
		SIF ABL:TARGET:1 >= 5 && ABL:TARGET:0 >= 5 && ABL:TARGET:9 >= 5 && EXP:TARGET:50 >= 7
			CALL EVENT_YOUJI
	;反発刻印３があり、かつ幼稚がない時
	ELSEIF TALENT:TARGET:151 == 0 && MARK:3 == 3
		;欲望が5かつ従順が5かつマゾっ気が5かつ異常経験が９以上に加え露出５とおもらし癖がある
		SIF ABL:TARGET:1 >= 5 && ABL:TARGET:0 >= 5 && ABL:TARGET:9 >= 5 && ABL:TARGET:8 >= 5 && EXP:TARGET:50 >= 9 && TALENT:57 == 1
			CALL EVENT_YOUJI
	ENDIF
ENDIF

;奴隷と道具の維持費の処理
;STARTモード限定でEASYでは20日、NORMALでは15日、HARDでは10日免除
A = 300
B = 25-FLAG:4*5
REPEAT 25
	SIF ITEM:COUNT
		A += 50
REND
A += CHARANUM*200
A -= 100
;労役中の奴隷は除く
REPEAT CHARANUM
	SIF CFLAG:COUNT:12 > 0
		A -= 200
REND
IF FLAG:5 == 0 && A && DAY > B
	PRINTFORML 奴隷と道具の維持費、生活費に${A}かかった……
	MONEY -= A
ENDIF

@EVENT_YOUJI
;幼児退行
PRINTFORMW %CALLNAME:TARGET%の様子がおかしい……
PRINTFORMW %CALLNAME:TARGET%は過酷な調教に耐え切れず、【幼児退行】してしまったようだ……
SIF TALENT:151
	TALENT:151 = 0
TALENT:150 = 1
MARK:3 = 0


;-------------------------------------------------
;朝になった時のイベント
;-------------------------------------------------
@EVENT_NEWDAY
;屋台の借金返済イベント
SIF FLAG:23 == 1 && MONEY >= 100000
	CALL RETURN_LOAN

;奴隷レンタルの期日が来た場合の奴隷返却処理
REPEAT CHARANUM
	IF CFLAG:COUNT:12 == 1 && CFLAG:COUNT:13 == DAY
		A = COUNT
		CALL RETURN_SLAVE
	ENDIF
REND

;娼館バイトが終わった場合の終了処理
REPEAT CHARANUM
	IF CFLAG:COUNT:12 == 2
		A = COUNT
		CALL END_PROSTITUTION
	ENDIF
REND

;全体の妊娠判定の処理
CALL IN_VAGINA

;全体の妊娠発覚時のイベント
CALL GET_CHILD

;排卵誘発剤と子宮内避妊結界の効果終了処理
REPEAT CHARANUM
	IF CFLAG:COUNT:108
		PRINTFORMW %CALLNAME:COUNT%の子宮内避妊結界の効果が切れたようだ
		CFLAG:COUNT:108 = 0
	ELSEIF CFLAG:COUNT:109
		PRINTFORMW %CALLNAME:COUNT%の排卵誘発剤の効果が切れたようだ
		CFLAG:COUNT:109 = 0
	ENDIF
REND

;出産･育児室関連
REPEAT CHARANUM
	IF TALENT:COUNT:153
		;出産3日前からは臨月突入で調教不可に
		IF (CFLAG:COUNT:110 - 3) == DAY
			DRAWLINE
			C = COUNT
			CALL REACH_FULL_TERM
		;出産前日には育児室への訪問を問うイベント発生
		ELSEIF (CFLAG:COUNT:110 - 1) == DAY && TFLAG:701 != DAY
			DRAWLINE
			PRINTFORML %CALLNAME:COUNT%は出産が近づいているようだ……
			C = COUNT
			CALL CHILD_CARE_ROOM_SELECT
		;出産予定日なら出産
		ELSEIF CFLAG:COUNT:110 == DAY
			DRAWLINE
			C = COUNT
			CALL CHILD_BIRTH
		ENDIF
	ELSEIF TALENT:COUNT:154
		;出産後翌日には育児室への訪問を問うイベント発生
		IF (CFLAG:COUNT:110 + 3) == DAY && TFLAG:701 != DAY
			DRAWLINE
			PRINTFORML %CALLNAME:COUNT%の出産から一日経ちました
			C = COUNT
			CALL CHILD_CARE_ROOM_SELECT
		;出産後3日には育児室への訪問を問うイベント発生
		ELSEIF (CFLAG:COUNT:110 + 3) == DAY && TFLAG:701 != DAY
			DRAWLINE
			PRINTFORML %CALLNAME:COUNT%の出産から三日経ちました
			C = COUNT
			CALL CHILD_CARE_ROOM_SELECT
		;出産5日で親離れで調教可能に
		ELSEIF (CFLAG:COUNT:110 + 5) == DAY
			DRAWLINE
			C = COUNT
			CALL DEPEARENT
		ENDIF
	ENDIF
REND

;-------------------------------------------------
;PCを選択式にしてみる
;-------------------------------------------------
@START_CHARA_SELECT
TFLAG:100 = 0

REPEAT 100
	TFLAG:COUNT = ITEMSALES:COUNT
	ITEMSALES:COUNT = 0
REND

CALL CHARA_LIST
REPEAT 30
	A = COUNT + TFLAG:100 * 30 + 200
	B = TFLAG:A
		ITEMSALES:B = 1
REND

CALL START_CHARA_SHOW

$INPUT_LOOP
INPUT
IF RESULT == 1
	IF TFLAG:100 > 0
		CALL CHARA_LIST
		REPEAT 30
			A = COUNT + TFLAG:100 * 30 + 200 - 30
			B = TFLAG:A
			ITEMSALES:B = 1
		REND
		TFLAG:100 -= 1
	ENDIF
	;バグ対策。キャラ上限を超えるとITEMSALES:0が登録されるので
	ITEMSALES:0 = 0

	CALL START_CHARA_SHOW
	GOTO INPUT_LOOP
ELSEIF RESULT == 9
	;最大ページ数と比較、ページ数変動の際は
	IF TFLAG:100 < 3
		CALL CHARA_LIST
		REPEAT 30
			A = COUNT + TFLAG:100 * 30 + 200 + 30
			B = TFLAG:A
			ITEMSALES:B = 1
		REND
		TFLAG:100 += 1
	ENDIF
	;バグ対策。キャラ上限を超えるとITEMSALES:0が登録されるので
	ITEMSALES:0 = 0

	CALL START_CHARA_SHOW
	GOTO INPUT_LOOP
ELSEIF RESULT == 0
	CALL CHARA_BUY_AFTER
ELSE
	C = RESULT + 1 - 100
	D = RESULT + 1000 - 100
	E = RESULT + 5000 - 100
	;キャラがいない＝フラグ値が0なら再入力
	SIF FLAG:D == 0
		GOTO INPUT_LOOP
	ADDCHARA C
	DELCHARA 0
	MASTER = 0
	FLAG:D = 0
	FLAG:E = 1

	PRINTFORMW プレイヤーを「%NAME:MASTER%」に設定します
	SIF NO:MASTER == 90
		CALL CHARA_RANDOM_STATE_M
	CALL CHARA_BUY_AFTER
ENDIF

@START_CHARA_SHOW
DRAWLINE
PRINTL ★★プレイヤーを変更できます★★
PRINTL ★★キャラを選択してください★★

PRINT_SHOPITEM

DRAWLINE
PRINT 　page
PRINTV TFLAG:100
PRINT 　[1]前のページ
PRINTC [0]変更しない
PRINTC [9]後のページ
PRINTL 



;-------------------------------------------------
;東方関連のイベント
;-------------------------------------------------
@EVENTTRAIN

;調教対象が慧音で時間が夜の時、1/3の確率で変身
IF TARGET >= 0
	IF NO:TARGET == 26 && TIME != 0 && RAND:3 == 0
		A = TARGET
		CALL HENSHIN
		DRAWLINE
		PRINTFORMW ＜慧音はハクタクの姿になった＞
	ENDIF
ENDIF

;助手が慧音で時間が夜の時、1/3の確率で変身
IF ASSI >= 0
	IF NO:ASSI == 26 && TIME != 0 && RAND:3 == 0
		A = ASSI
		CALL HENSHIN
		DRAWLINE
		PRINTFORMW ＜慧音はハクタクの姿になった＞
	ENDIF
ENDIF

@HENSHIN
;変身フラグにTEQUIP:10を使用。変身での素質変更を行なう。
IF TEQUIP:A:10 == 0
	;変身時(ハクタク化)
	TEQUIP:A:10 = 1
	REPEAT 200
		L = COUNT + 600
		;素質の消滅処理。既に消滅している場合、PBAND:600～799に1を立てるだけで消滅しない
		;消滅素質：自制心(20)、保守的(24)、一線を越えない(27)、貞操観念(30)、濡れにくい(43)、人気(145)
		IF COUNT == 20 || COUNT == 24 || COUNT == 27 || COUNT == 30 || COUNT == 43 || COUNT == 145
			IF TALENT:A:COUNT == 0
				PBAND:L = 1
			ELSE
				PBAND:L = 0
				TALENT:A:COUNT = 0
			ENDIF
		;素質の習得処理。既に習得している場合、PBAND:600～799に2を立てるだけで習得しない
		;習得素質：解放(33)、濡れやすい(42)、汚れ無視(64)、両刀(81)、月光浴(134)
		ELSEIF COUNT == 33 || COUNT == 42 || COUNT == 64 || COUNT == 81 || COUNT == 134
			IF TALENT:A:COUNT == 1
				PBAND:L = 2
			ELSE
				PBAND:L = 0
				TALENT:A:COUNT = 1
			ENDIF
		ENDIF
	REND
ELSE
	;解除時(ハクタク解除)
	TEQUIP:A:10 = 0
	REPEAT 200
		L = COUNT + 600
		;変身解除時、習得処理を行なう(消滅していた素質は除く。PBAND:600～799に1が立っていたら習得処理を飛ばす)
		;習得素質：自制心(20)、保守的(24)、一線を越えない(27)、貞操観念(30)、濡れにくい(43)、人気(145)
		IF COUNT == 20 || COUNT == 24 || COUNT == 27 || COUNT == 30 || COUNT == 43 || COUNT == 145
			SIF PBAND:L == 0
				TALENT:A:COUNT = 1
		;変身解除時、消滅処理を行なう(習得していた素質は除く。PBAND:600～799に2が立っていたら消滅処理を飛ばす)
		;消滅素質：解放(33)、濡れやすい(42)、汚れ無視(64)、両刀(81)、月光浴(134)
		ELSEIF COUNT == 33 || COUNT == 42 || COUNT == 64 || COUNT == 81 || COUNT == 134
			SIF PBAND:L == 0
				TALENT:A:COUNT = 0
		ENDIF
	REND
ENDIF

@EVENT_NEXTDAY_TOHO
;調教対象が慧音(ハクタク)の時、元に戻る
IF TARGET >= 0
	IF NO:TARGET == 26 && TEQUIP:10
		A = TARGET
		CALL HENSHIN
		PRINTFORMW ＜慧音は元の姿にもどった＞
	ENDIF
ENDIF

;助手が慧音(ハクタク)の時、元に戻る
IF ASSI >= 0
	IF NO:ASSI == 26 && TEQUIP:ASSI:10
		A = ASSI
		CALL HENSHIN
		PRINTFORMW ＜慧音は元の姿にもどった＞
	ENDIF
ENDIF

;調教対象がぬえのときにランダムで敏感、鈍感系の素質が変動する
IF TARGET >= 0
	IF NO:TARGET == 73
		A = RAND:5
		IF A == 0
			;敏感、鈍感の変動処理
			REPEAT 4
				A = RAND:10
				B = 101 + COUNT*2
				C = 102 + COUNT*2
				IF A >= 8
					TALENT:B = 0
					TALENT:C = 1
				ELSEIF A <= 1
					TALENT:B = 1
					TALENT:C = 0
				ELSE
					TALENT:B = 0
					TALENT:C = 0
				ENDIF
			REND
			PRINTFORMW ＜ぬえの素質が変動しました＞
		ENDIF
	ENDIF
ENDIF



